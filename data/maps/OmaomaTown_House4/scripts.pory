mapscripts OmaomaTown_House4_MapScripts {}

script OmaomaTown_House4_EventScript_Trader {
	lock
	faceplayer

	if (flag(FLAG_OMAOMA_NPC_TRADE_COMPLETED)) {
		msgbox(format("Thanks again.\pI hope you're less creeped out by that Pikachu than I was."))
		release
		end
    }

	setvar(VAR_0x8008, INGAME_TRADE_MIMIKYU)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)

	msgbox(format("I got this really weird Pikachu…\pIt makes me uncomfortable.\p I'd really like an {STR_VAR_1} instead.\pWould you like to trade your {STR_VAR_1} for my…Pikachu…?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO) {
		msgbox(format("Aw man…"))
		release
		end
    }

	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)

	if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		msgbox(format("You don't really want to trade? Aw man…"))
		release
		end
    }

	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)

	if(var(VAR_RESULT) != VAR_0x8009) {
		bufferspeciesname(STR_VAR_1, VAR_0x8009)
        msgbox(format("Huh? That's not an {STR_VAR_1}…"))
		release
		end
    }

	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate

	// show message saying thanks or w/e
	msgbox(format("Awesome! I'll finally be able to get some sleep without feeling like I'm being watched."))
	setflag(FLAG_OMAOMA_NPC_TRADE_COMPLETED)

	release
	end
}